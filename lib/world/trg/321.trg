#32100
начало квеста~
0 q 100
~
wait 1s
mecho Старушка подняла на вас глаза и слезинка упала ей на колени.
wait 1s
г Ах ты ж, господи, у меня гости, а у меня и угостить то нечем...
mecho Старушка попыталась приподнятся в кресле, но у нее ничего не получилось. 
плак
wait 1s
г Сил совсем не осталось...
mecho Старушка умолкла и сложила руки на коленях.
attach 32101 %self.id%
detach 32100 %self.id%
~
#32101
продолжение начала квеста~
0 d 1
да продолжай слушаю~
if %actor.vnum% != -1
  halt
end
*set questor321 %actor%
*global questor321
wait 1
г Внучка у меня пропала, Герда... Как-то утром проснулась, а ее нет. 
г Теперь за что не возьмусь, все из рук падает. Все о ней думаю. Где она сейчас, что делает.
wait 1
г Наверное пошла искать глупышка моя соседского мальчишку, Кая. 
г Пропал как-то зимой, мальчишки говорили потом, что видели что увезли его какие-то большие белые сани...
wait 1
г А может утонул, кто его знает...
плак
wait 2
mecho Старушка вдруг встрепенулась и выпрямившись в кресле с надеждой глянула на вас.
г Прошу вас, пожалуйста, найдите мою девочку! Я ничего для вас не пожалею!
wait 1
mecho Немного приходя в себя, старушка обвела нищую комнату растеряным взглядом и понурилась.
г Правда, особо ничего у меня нет, но к вашему возвращению я подыщу что-нить полезное!
attach 32122 %self.id%
detach 32101 %self.id%
~
#32102
смерть старухи-колдуньи~
0 f 100
~
calcuid house 32135 room
attach 32103 %house.id%
~
#32103
Девочка Герда проснулась от магического сна~
2 e 100
~
wait 1
wecho Девочка приподнялась в кровати и потянулась.
calcuid gerda 32111 mob
wpurge %gerda%
wload mob 32110
wecho Герда сказала : 'Ааах... Как долго я спала... Какие волшебные сны мне снились!'
wait 1
wecho Девочка вдруг забескоилась и воскликнула:
wecho _- Господи, а как же Кай? Где он? Я так и не нашла его!
wait 3
wecho Малявка вдруг вскочила с кровати, и подбежала прямо к вам.
if %actor.sex% == 1
  wecho Герда сказала : 'Ой, спасибо вам, дядечка, что разбудили! Побежала я братца своего названного Кая искать!'
else
  wecho Герда сказала : 'Ой, спасибо вам, тетечка, что разбудили! Побежала я братца своего названного Кая искать!'
end
wecho Быстрей молнии девочка скрылась за дверью, оставив вас в слегка ошарашенном состоянии.
wecho "Могла бы и причесаться", - неодобрительно подумали вы. 
calcuid gerda 32110 mob
wpurge %gerda%
wat 32149 wload mob 32110
calcuid gerda 32110 mob
attach 32104 %gerda.id%
detach 32103 %self.id%
~
#32104
Герда уже добралась до замка~
0 q 100
~
wait 1
mecho Девочка беспомощно оглянулась на вас. 
г Там, в замке, Кай! я точно знаю что он там!
wait 1
г Но там кто-то есть еще! Я их чувствую и очень, очень боюсь!
wait 1
mload obj 32112
say Вороны дали мне ключ... Вот этот.
дать ключ .%actor.name%
say ...но они сказали что в замке поселились привидения! Мне страшно...
mecho Глаза девочки налились слезами.
mecho Вы всерьез стали опасаться за душевное состояние малышки.
detach 32104 %self.id%
~
#32105
одуванчик подлый слепит ~
0 q 75
~
wait 1
if (!%actor% || (%actor.level% > 30))
  halt
end
mecho Одуванчик качнулся от ветра, выпуская в Вас тучу легкого пуха! 
mecho Поздравляем, Вы ослепли!
*кастинг по тригу на полтика !слепота! рэндомно
dg_affect %actor% слепота слепота 3 30 
~
#32106
Агр колдуньи~
0 q 100
~
wait 1
mecho Глаза старухи налились кровью и она заорала скрипящим голосом: 
mecho _- Девочку-сиротинушку мою забрать вздумали!? Вот я вам!
mecho Старуха со всей дури замахнулась на вас своей клюкой.
mkill %actor%
~
#32107
колючие заросли~
2 be 80
~
wait 1
set target %random.pc%
if !%target%
  halt
end
wsend %target% _Проклятые колючки исцарапали вас до крови!
wechoaround %target% _~~%target.name% оцарапал%target.u% о колючки и громко вскрикнул%target.g%.
wdamage %target% 50
~
#32108
лапландка~
0 d 1
здравствуй приветствую поклон~
wait 1
mecho Старая лапландка неторопливо огляделась,
mecho отложила как раз дожаренную рыбу в сторону и подняла на вас глаза.
wait 1
say Хм... То не было гостей долго, то один за другим.
say Совсем недавно девочка маленькая приезжала на олене, искала...
mecho Лапландка испуганно огляделась и наклонилась к вам поближе.
wait 1
say Она искала саму Снежную Королеву!
say Вы, небось, тоже ее ищите?
attach 32109 %self.id%
detach 32108 %self.id%
~
#32109
лапландка посылает вас~
0 d 1
да угу ищем ага~
wait 1
взд
wait 1s
say Так и быть, и вам укажу дорогу. Вдруг девочке помощь требуется, она ведь такая кроха...
mecho _- Сейчас идите на восток, а там увидите тропинку. Вот по ней и идите до конца.
mecho _- Там живет финка, старая ведунья. Она вам точно покажет как пройти туда куда вам нужно.
mecho _- Только там волки шныряют, будьте уж осторожны.
взд .%actor.name%
*открывается проход в комнату 32184*
mdoor  32181 east room 32184
mdoor  32184 west room 32181
detach 32109 %self.id%
~
#32110
просыпается следующий разб~
0 r 100
~
wait 5
mecho Безжизненное тело валяющегося на полу разбойника слегка пошевилось, всхрапнуло и 
вст
mecho Пошатываясь и чертыхаясь, еле сфокусировав глаза,
mecho разбойник изобразил на лице тоскливое неудоумение и бросился на вас!
set target %random.pc%
mkill %target%
~
#32111
Растеряный горожанин бубнит~
0 q 90
~
wait 1
say Ну где же оно┘ может здесь...
wait 2s
say или вот там...
emot наклонился, и начал ощупывать мостовую руками
wait 2s
say о, бедный я, несчастный...
тоск
~
#32112
врывается герда~
2 e 100
~
*если убили всех призраков*
if (%exist.mob(32130)% || %exist.mob(32132)% || %exist.mob(32133)% || %exist.mob(32147)%)
  halt
end
wait 1 
wload mob 32148
calcuid gerda 32148 mob
wecho В комнату вбежала Герда, и замерла на пороге, не отрывая взора от юного короля. Ее
wecho глаза, сияющие надеждой, вдруг наполнилась слезами.
wait 1s
wforce %gerda% понур
wforce %gerda% say А вы вовсе не Кай... 
wforce %gerda% плак
wecho _- А я так надеялась найти его! - воскликнула девочка.
calcuid king 32123 mob
exec 32118 %king.id%
detach 32112 %self.id%
~
#32113
смерть снежных существ~
0 f 100
~
mecho %self.name% вдруг взметнул%self.u% вам в лицо снежным вихрем и опал%self.a% на землю стаей белых снежинок.
~
#32114
просыпается пьянчуга-разб~
0 q 0
~
wait 1
mecho Безжизненное тело валяющегося на полу разбойника слегка пошевилось, всхрапнуло и 
вст
mecho Пошатываясь и чертыхаясь, еле сфокусировав глаза,
mecho разбойник изобразил на лице тоскливое неудоумение и бросился на вас!
set  target %random.pc%
attack %target.name%
~
#32115
Герда удирает на олене~
0 z 100
~
wait 1
msend %killer% Вы развязали малышке руки и оглядели ее на предмет повреждений.
msend %killer% Изо-всех сил сдерживая желание отшлепать Герду, вы отряхнули ее от соломы.
mechoaround %killer% ~~%killer.name% развязал%killer.g% Герде руки и отряхнул%killer.g% от соломы.
wait 1s
mecho Девочка рукавом провела по своему симпатичному личику, стирая дорожки, оставленные слезами.
mecho Девочка смущенно улыбнулась вам.
say Простите, мне надо искать Кая... Олешек говорит, что знает, где он.
кив олень
mecho Герда развязала оленя, вскочила ему на спину и крепко обняла его за шею.
say Прощайте, спасибо вам!
mecho Олень, качнув головой одним прыжком вылетел из комнаты.
*открывается проход на юг в комнату 32180
mdoor  32177 south room  32180
mdoor  32180 north room  32177  
*удалям Герду и оленя
calcuid deer 32119 mob
mpurge %deer%
wait 1
mpurge %self%

~
#32116
мал.разбойница агрит Герду~
0 q 100
~
wait 1
mecho Маленькая разбойница нахмурилась и взгянула на Герду. 
say Ты позвала на помощь? В благодарность за то, что я оставила тебя живой?
say Ах ты мерзавка!
kill gerda32100
mtransform 32152
attach 32117 %self.id%
detach 32116 %self.id%
~
#32117
Убита девка-разбойница~
0 f 100
~
if %actor.vnum% == -1
  set killer %actor%
else
  set killer %actor.leader%
end
if !%killer%
  halt
end
global killer
calcuid gerda 32149 mob
remote killer %gerda.id%
run 32115 %gerda.id%
~
#32118
Маленький король раздает подарки~
0 z 100
~
wait 1
say Честно говоря, я ничего не понимаю. Ничего, кроме того, что вы освободили замок от 
теней...
поклон
Молодой король посмотрел на Герду. 
say Будь добра, расскажи, кто такой Кай, и почему ты его ищешь.
wait 4s
mecho Не прошло и часа, как промокая слезы огромным носовым платком, что ей протянула
mecho чья-то рука из-за балдахина кровати, Герда рассказала всю свою душещипательную историю.
wait 1s
mecho Молодой король взволнованно произнес:
mecho _- Потрясающая история! Герда, позволь мы одарим тебя теплой одеждой,
mecho _- дальше уже будет довольно прохладно! 
mecho Мальчик вытащил откуда-то теплую шубку, шапку и чудесную муфточку,
mecho чтобы Герда смогла согреть руки.
mecho Молодой король вручил все это добро Герде и повернулся к вам. 
calcuid gerda 32148 mob
mpurge %gerda%
calcuid gerda 32110 mob
mpurge %gerda%
say Вы спасли нас от этих ужасных призраков. Мммм.. как же мне отблагодарить вас...
дум
mecho Лицо короля просветлело. 
say О, у нас есть кое-что для вас!
mecho Мальчик отошел в темный угол залы, чем-то там пошуршал, оглядываясь,
mecho и пришел, неся в руках небольшой сверток.
say У нас осталось много драгоценностей, которые мы бы хотели вручить вам!
*вручает драгоценности в размере 5000 кун*  
mload obj 32108
mload obj 32109
give драгоц .%actor.name%
give ключ .%actor.name%
say А это ключ от ворот на дороге в лес.
say Благодарю Вас и прощайте.
wait 1s
mecho Только теперь вы заметили, что Герда уже успела ускользнуть из зала.
~
#32119
финка показывает дорогу~
0 q 100
~
wait 1
mecho Финка взглянула на вас и отвернулась.
ворч
wait 1
г И вы туда же...
вздох
эмо взяла какой-то свиток с полки и принялась внимательно изучать его.
wait 1
вздох
г Хмм.. Ну что же... может быть... 
wait 1
г Ладно. Ступайте по тропинке до замерзшего озера, а там увидите небольшую тропинку на юг
по льду. 
г Будьте осторожны. На юге вы и увидите, если дойдете, конечно, чертоги Снежной Королевы.
г Ступайте уже, не медлите.
*открывается проход с клетки 32181 на клетку 32186*
mdoor  32194 south room  32187
mdoor  32187 north room  32194 
calcuid castle 32178 room
attach 32120 %castle.id%
detach 32119 %self.id%
~
#32120
Герда находит Кая~
2 e 100
~
wait 1
wload mob 32151
wecho Из-за двери выбежала Герда и кинулась к мальчику.
wecho Герда воскликнула: Кай! Кай! Я нашла тебя!
wait 1
wecho Девочка кинулась обнимать и целовать мальчика, но тот сидел недвижимый как статуя, 
wecho продолжая собирать свои льдинки.
wait 1
wecho Герда горько заплакала. Ее слезинки упали ему мальчику на грудь и растопили лед в его сердце.
wait 1
wecho Кай вдруг огляделся, узнал Герду и крепко-крепко обнял ее и заплакал. 
wecho Вы с умилением засмотрелись на эту картину.
wait 2
wecho Вдруг в чертогах поднялась снежная буря, швыряя вам в глаза колючие льдинки, и из снега 
wecho предстала перед вами сама Снежная Королева, прекрасная и величественная!
wecho Герда испуганно вскрикнула, схватила Кая за руку и они вместе спрятались за троном.
calcuid gerda 32151 mob
calcuid kay 32150 mob
wpurge %gerda%
wpurge %kay%
wload mob 32100
detach 32120 %self.id%
~
#32121
Дети следуют за вами~
0 d 1
следуйте пошли пойдемте идем пойдем~
wait 1
mecho Дети взялись за руки, переглянулись радостно и пошли за вами.
follow .%actor.name%
detach 32121 %self.id%
~
#32122
Бабуля видит внучку. Конец~
0 q 100
~
wait 1
calcuid gerda 32154 mob
calcuid kay 32155 mob
if ((%kay.realroom% != 32103) || (%gerda.realroom% != 32103))
  halt
end
wait 1
mecho Бабушка подняла глаза на вошедших и охнула: 
г Герда!!! Внученька моя!
mecho Герда кунулась к бабушке и села возле нее на скамеечку, глядя полными слезами глазами на старушку. 
mecho Герда сказала: Я нашла Кая, бабушка!
mforce %gerda% follow я
mecho Бабушка, плача теперь уже от радости, почти ничего не видя от слез, достала что-то из шкафчика.
switch %random.20%
  case 1
    case 2
      case 3
        mload obj 555
        дать книг .%actor.name%
      break
      case 4
        case 5
          case 6
            mload obj 410
            дать свиток .%actor.name%
          break
          case 7
            if (%world.curobjs(215)% < 50)
              mload obj 215
              give рун .%actor.name%
            else
              mload obj 32113
              give роза .%actor.name%
            end
          break
          default
            mload obj 32113
            give роза .%actor.name%
          done
          detach 32122 %self.id%
~
#32123
смерть Снежки~
0 f 100
~
if %world.curobjs(32107)% < 1 && %random.1000% < 150
  mload obj 32107
end
mload mob 32154
mload mob 32155
~
#32124
Мини-квест у горожанина~
0 d 1
помогу~
detach 32111 %self.id%
say  Нет никакой надежды! Я уже все обыскал!
emot начинал делать отчаянные попытки побиться головой о мостовую
wait 1s
say Я сегодня чуть-чуть
mecho Горожанин делает неопределенные движения пальцами
say ...выпил, и чтобы не ругалась моя женушка, (испуганно озирается) решил
mecho _- купить ей колечко. Но только оно пропало! Где-то обронил, по-видимому!
рыда
wait 1s
say Хоть домой не возвращайся!   
attach 32125 %self.id%
detach 32124 %self.id%
~
#32125
Вернули колечко горожанину~
0 j 100
~
wait 1
if %object.vnum% != 32110
  дум
  ик
  drop all
  halt
end
wait 1
mpurge %object%
wait 2s
mecho Лиуцо горожанина озарилось неземным блаженством и облегчением. 
mecho _Гражданин положил колечко в карман.
mecho Чуть подпрыгивая от радости, мужчина кинулся вас обнимать.
wait 2
say  Чем же мне вас отблагодарить?
дум
wait 1s
mecho Лицо горожанина посветлело.
say Я тут пока колечко искал, нашел одну вещицу! Прекрасная работа! Старые мастера! 
mload obj 32111
give ключ .%actor.name%
mecho Горожанин быстро скрылся в подворотне.
wait 1
mpurge %self%
~
#32126
Убили воришку~
0 f 100
~
mload obj 32110
~
#32127
Королева агрит~
0 n 100
~
wait 1
say Жалкие людишки! 
say Как вы посмели осквернить своим присутствием чистоту моих залов!
set target %random.pc%
mkill %target%
~
#32128
Герда следует за игроком~
0 n 100
~
wait 2
mecho Увидев, что все утихло, Кай и Герда выскочили из-за трона.
mecho Дети взялись за руки, переглянулись радостно и пошли за вами.
set %target% %random.pc%
follow .%target.name%
~
#32129
Кай следует за гердой~
0 n 100
~
wait 1
follow gerda32101
~
#32130
Мобы фликают~
0 k 75
~
flee
flee
~
#32131
Репоп зоны "Снежная королева"~
2 f 100
~
calcuid house 32135 room
detach 32103 %house.id%
wdoor  32181 east purge
wdoor  32184 west purge
calcuid castle 32155 room
attach 32112 %castle.id%
wdoor  32194 south purge
wdoor  32187 north purge
~
#32132
Колдунья зашла к игроку~
0 t 100
~
wait 1
mecho Глаза старухи налились кровью и она заорала скрипящим голосом: 
mecho _- Девочку-сиротинушку мою забрать вздумали!? Вот я вам!
mecho Старуха со всей дури замахнулась на вас своей клюкой.
set target %random.pc%
mkill %target%
~
$~
