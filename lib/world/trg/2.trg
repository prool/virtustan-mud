#200
запуск поезда~
2 c 100
старт~
* внимание: все добавления вносить в концы списков ниже
if %actor.level% < 31
  %send% %actor% ты мне не балуй тут, это только богам дозволено
  halt
else
  %send% %actor% своей могучей волей вы запустили поезд
end
* начальная точка движения поезда, не трогать
set point 1
* список направлений, по которым можно выйти из поезда на станцию, писать по-английски и желательно полностью: north south west east up down
set dir down down down
* список направлений, по которым можно будет войти в поезд со станции, заполнять тоже по-английски и желательно не сокращая слов
set pdir up up up
* список названий станций, видны при объявлениях в поезде
set place небеса харьков остров_Толк
* список внумов станций
set vplace 207 9903 21000
calcuid train 208 room
detach 201 %train%
attach 201 %train%
run 201 %train%
~
#201
движение поезда~
2 z 100
~
if %point% == 0
  %echo% внезапно поезд остановился, и вы поняли, что он больше не сдвинется с места
  halt
  * сколько пунктов в списках триггера 200, столько же должно стоять после знака > в строке ниже
elseif %point% > 3
  %echo% маршрут пройден, начинаем заново
  set point 1
end
* следующие 4 строки- переменные, обозначающие те элементы списков из тригга 200, которые должны быть задействованы на данной точке, их не трогать
set dir2 %dir.words(%point%)%
set pdir2 %pdir.words(%point%)%
set place2 %place.words(%point%)%
set vplace2 %vplace.words(%point%)%
* время стоянки, в минутах, для отображения
eval pause %random.3%
* время стоянки, в секундах, для установки вейта в триггере
eval pause2 %pause%*60
%echo% следующая остановка- %place2%, произнес приятный женский голос
wait 30s
* после приезда поезда объявляем, добавляем проходы на станцию из поезда и из поезда на станцию, загружаем предмет, ждем, убираем предмет и проходы, объявляем и уезжаем
%echo% поезд остановился, открылась дверь и вниз спустилась полупрозрачная лесенка
%door% 208 %dir2% room %vplace2%
%door% %vplace2% %pdir2% room 208
* загружаем предмет, показывающий, что здесь стоит поезд
wat %vplace2% %load% obj 251
calcuid lestnica 251 obj
wat %vplace2% %echo% внезапно в воздухе перед вами возник небесный поезд, дверь его распахнулась, и вниз опустилась полупрозрачная лесенка
%echo% остановка %place2%, стоим %pause% минут(ы)- объявил приятный женский голос
wzoneecho %vplace2% внимание внимание, прибыл небесный поезд, стоянка %pause% минут(ы)
wait %pause2%s
%echo% лестница пропала и дверь закрылась, после чего вы отправились дальше по маршруту
wat %vplace2% %purge% %lestnica%
wat %vplace2% %echo% вдруг лестница пропала, дверь закрылась, и поезд унесся в неизвестном направлении
%door% 208 %dir2% purge
%door% %vplace2% %pdir2% purge
wzoneecho %vplace2% небесный поезд отбывает немедленно
eval point %point% +1
global point
run 203 %self%
~
#202
остановка поезда~
2 c 100
стоп~
if %actor.level% < 31
  %send% %actor% такое только богам позволено, а вы явно к ним не относитесь
  halt
else
  %send% %actor% странное решение, но жираф большой- ему видней
end
set point 0
calcuid train 208 room
detach 201 %train%
attach 201 %train%
run 201 %train%
~
#203
возобновление движения~
2 z 100
~
wait 1
run 201 %train%
~
$~
