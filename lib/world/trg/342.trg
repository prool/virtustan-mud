#34200
залезть на вышку~
2 c 0
лезть залезть~
*игрок ввел залезть или лезть вышка
*в зависимости от того в какой комнате находился игрок
*тригер перемещает его в соответсвующую комнату
*в зоне 3 вышки но тригер перемещения 1
if (%arg.contains(верх)%)||(%arg.contains(вышк)%)
  wsend       %actor% - Вы полезли вверх на вышку.
  wechoaround %actor% %actor.name% полез%actor.q% вверх на вышку.
  wait 1s
  wsend %actor% - После недолгого подъема вы оказались на верху.
  if (%actor.realroom%==34210)
    wteleport %actor% 34233 
  elseif (%actor.realroom%==34221)
    wteleport %actor% 34246
  else
    wteleport %actor% 34273 
  endif
  wat %actor.realroom% wechoaround %actor% %actor.name% пробрал%actor.u% сюда.
end
~
#34201
слезть с вышки~
2 c 0
лезть слезть~
*игрок на вышке ввел лезть вниз или слезть вниз
*вышек 3 тригер 1. поэтому тригер определяет на какой вышке находится игрок
if (%arg.contains(вниз)%)||(%arg.contains(вышк)%)
  wsend       %actor% - Вы начали осторожно спускаться с вышки.
  wechoaround %actor% %actor.name% полез%actor.q% вниз с вышки.
  wait 1s
  wsend %actor% - После недолгого спуска вы оказались внизу.
  if (%actor.realroom%==34233)
    wteleport %actor% 34210
  elseif (%actor.realroom%==34246)
    wteleport %actor% 34221
  else
    wteleport %actor% 34254 
  endif
  wat %actor.realroom% wechoaround %actor% %actor.name% спустил%actor.u% с вышки сюда.
end
~
#34202
осмотреть вышку~
2 c 0
осмотреть~
*тригер помогающий игроку найти вход на вышку
if (%arg.contains(вышк)%)
  wsend   %actor% Сторожевая вышка. На ее вершину ведет лестница, по которой можно залезть наверх.
end
~
#34203
войти в шалаш~
2 c 0
лезть войти~
*вход в шалаш. если жив крестьянин охраняющий вход в шалаш
*тригер не отрабатывает. Должен отработать тригер крестьянина
if (%arg.contains(внутр)%)||(%arg.contains(шалаш)%)
  if %exist.mob(34203)%
    return 0
  else
    wsend       %actor% - Вы присели и вошли в шалаш.
    wechoaround %actor% %actor.name% присел%actor.g% и полез%actor.q% в шалаш.
    wait 1s
    wsend %actor% - Вы в шалаше, интересно стоило сюда лезть?
    wteleport %actor% 34252
    wat 34252 wechoaround %actor% %actor.name% пробрал%actor.u% сюда.
  end
end
~
#34204
войти в шалаш~
0 c 0
лезть войти~
*тригер крестьянин охраняющего вход в шалаш.
*Он какбы не пускает игрока внутрь (на самом деле тригер входа в шалаш не работает пока жив крестьянин)
if (%arg.contains(внутр)%)||(%arg.contains(шалаш)%)
  г Ты куда лезешь?
  г А ну, иди отсюда! Пока по шее не дал!
  wait 2s
  убрать все
  if !%self.haveobj(34208)%
    if %world.curobjs(34208)%<4
      mecho Крестьянин достал из-за пазухи острой нож.
      %load% obj 34208
      вооруж нож
      держ нож
    endif
  end
  wait 2s
  г Иди от сюда по добру, по здорову...
  wait 2s
  думать %actor%
  wait 1s
  г Я предупредил!
  ухм %actor%
  wait 4s
  встать
  убить %actor.name%
end
~
#34205
Блуждание по березняку~
2 g 100
1~
*простенький тригер не несущий практической нагрузки
if %direction%==south
  wsend %actor% - Вы начали усиленно продираться сквозь березняк. 
  wsend %actor% - Но пройдя немного вы поняли, что заблудились и решили вернуться назад.
endif
~
#34206
Помощь лучников~
0 k 25
~
*Стражникам с этим тригером помогают лучники сидящие в остроге.
*По замыслу Лучники сидят в остроге и стреляют из бойниц. прямого участия в бое они не принимают
*У стражников есть ключ который надо получить игроку. но ключ можно украсть, а не убивать стражников
*Надо проверить будет ли проходить повреждения по игроку, наносимые этим тригером
*а именно  %damage% %actor% 50. У меня в локальном маде повреждения не наносились
if %exist.mob(34205)%
  msend %actor% Стрела, выпущенная из бойницы, острога попала Вам в грудь!
  msend %actor% Это было БОЛЬНО!
  mechoaround %actor% Стрела, выпущенная из бойницы острога, вонзилась %actor.dname% в грудь.
  mdamage %actor% 50
end
~
#34207
смерть ~
0 f 0
~
*эээ... не используемый тригер 
msend %actor% Вы убили %self.iname% убийцу завут %actor.iname%
~
#34208
в случае нападение на Тракте вызывает подмогу~
0 l 200
~
halt
*Тригер переброски патруля на поможь атакованому играком мобу
*mecho !!!!! triger
*Патруль защищает мобов только на тракте. если моб не на тракте то патруль не приедет.
*Ну по идее наблюдатель с вышки видит только тракт. а лес и поля рядом с трактом он не просматривает.
eval komnata %self.realroom%
set temp %komnata.name%
if !(%temp.contains(тракт)%)
  halt
end
*проверяем живы ли наблюдателыи на вышках
*на тракте 3 вышки с наблюдателями, каждый наблюдатель просматривает свой участок тракта
* и если его убить то участок дороги останестя без помощи патруля :)
if (%self.realroom%>34246)
  * mecho !!!!! участок 1
  if !%exist.mob(34210)%
    halt
  end
elseif (%self.realroom%<34223)&(%self.realroom%>34213)
  * mecho !!!! участок 2
  if !%exist.mob(34209)%
    * mecho !!!!! Моб 34209 не существует выходим из тригера.
    halt
  end
elseif (%self.realroom%<34214)
  * mecho !!!! участок 3
  if !%exist.mob(34208)%
    halt
  end
end
*собственно непосредственно процедура переброски дружинников из острога 
*в комнату где происходит драка
*дружинники патруля должны сами вступить в бой, так как они хэлперы
if !%exist.mob(34206)% 
  attach 34210 %self.id%
  detach 34208 %self.id%
  halt
end
calcuid drugin 34206 mob
set target %self.fighting%
foreach char %drugin.npc%
  mechoaround %drugin% %drugin.name% схватился за оружие и выскочил из комнаты
  if %char.vnum% == 34206
    mteleport %char% %self.realroom%
    %force% %char% kill %target.name%
  end
done
mteleport %drugin% %self.realroom%
%force% %drugin% kill %target.name%
detach 34208 %self.id%
attach 34210 %self.id%
~
#34209
возврат дружинников в барак~
0 b 100
~
* дружинники после боя, должны вернуться назад в барак...
*%echo% _тригер!!!
if (%self.realroom%==34292)
  * %echo% _я на базе!!!
  halt
endif
if (%self.fighting%)
  * %echo% _Дерусь!!!!!!!!!!!
else
  %echo% %self.iname% сел на коня и ускакал в острог...
  %teleport% %self% 34292
endif
~
#34210
возврат тригера 34208~
0 b 100
~
*какаято фигня... сделал чтобы тригер вызова патруля не запускался каждый 
*раунгд боя, дабы неперегружать сервер.
if (%self.fighting%)
  halt
endif
detach 34210 %self.id%
attach 34208 %self.id%
*mecho !!!! отключили 34210 подключили 34208
~
#34211
осмотреть карту~
2 c 0
осмотреть~
*в остроге в комнате командира на столе лежит карта. на которой нарисован тракт 
*с укзазнием трех вышек. Посмотрев ее игрок должен догадаться что есть более 
*простой способ обезвредить патруль, вырезав наблюдателей на вышках
if (%arg.contains(карт)%)
  if %exist.mob(34211)%
    %send% %actor% _Командир заслонил от Вас карту.
    halt	
  endif
  %send% %actor% _Вы внимательно осмотрели карту.
  %send% %actor% _на ней изображены окрестности и пометки, сделанные красными чернилами.
  %send% %actor% _               
  %send% %actor% _                дозорная  Разбойники 
  %send% %actor% _    дозорная     вышка      Х
  %send% %actor% _     вышка         П           дозорная   +
  %send% %actor% _       П    ===========         вышка     П
  %send% %actor% _    ========   тракт   ========== П    ! !=!
  %send% %actor% _  ==                   _   _     ======# КИЕВ
  %send% %actor% _                       ШШПШШ           !
  %send% %actor% _                       острог
  %send% %actor% _
else
  %send% %actor% _Осмотреть что?
endif 
~
#34212
Сообщение при вооружении мечем~
1 j 100
~
wait 1
%echoaround% %actor% %actor.name% повертел%actor.g% меч в руке привыкая нему.
%send% %actor% Вы повертели меч в руке привыкая нему
~
#34213
Моб поднимает трупы~
0 b 100
~
*по тригеру должны подниматься только трупы лежащие в одной комнате с черноком.
*Но почемуто поднимат даже трупы лежащие в других комнатах зоны :(
*Надо проверить если это так и есть то лучше убрать тригер у моба 34214
%echo%  Чернокнижник засветился таинственным светом.
взять все труп
одеть все
бросить все
взять труп
if %self.haveobj(труп)%
  бросить все
  dg_cast 'подн труп' труп
  attach 34214 %self.id%
end
~
#34214
приказывает помочь себе в бою~
0 k 100
~
*тригер относится к черноку.
*приказывет поднятым созданиям убить врага
*%echo% приказал всем помоч!!!!
приказ всем убить %actor.name%
detach 34214 %self.id%
~
#34215
Разговаривающий череп~
1 j 100
~
*и так понятно что делает тригер
*вооружиться может только Чернокнижник.
oechoaround %actor% Череп сверкнул пустыми глазницами и что-то пробормотал %actor.dname%.
osend %actor% Череп повернулся к вам на посохе и сверкнул пустыми глазницами.
if (%actor.class% != 8)
  osend %actor% Череп прошипел Вам : 'Кто ты такой чтобы тлевожить меня пошле шмерти?'
  osend %actor% Череп прошипел Вам : 'А ну убели свои лученки от моего нашешта, а то пожелееш!'
  osend %actor% Череп сделал попытку укусить Вас за палец! Подлец!
  osend %actor% Какой-то нервный тип. Лучше оставить его в покое.
  %echoaround% %actor% Череп укусил %actor.vname% за палец.
  return 0
  halt
end
if %self.worn_by%
  return 1
  halt
end
wait 1
if (%actor.sex% == 1)
  osend %actor% Череп прошипел Вам : 'Пливештвую тебя челный блат %actor.iname%!'
elseif (%actor.sex% == 2)
  osend %actor% Череп прошипел Вам : 'Пливештвую тебя челная сестла %actor.iname%!'
else
  osend %actor% Череп прошипел Вам : 'Пливештвую тебя челная душа %actor.iname%!'
end
osend %actor% Череп прошипел Вам : 'Я буду шлужить тебе, пока ты не покинеш меня.'
~
#34216
Убили стражника (лоад ключа)~
0 f 100
~
if %world.curmobs(34204)% < 2
  mload obj 34201
end
~
$~
