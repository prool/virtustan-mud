#38400
померла рыбка у БУЯНА~
0 f 100
~
mload obj 33111
~
#38401
игрок входит к черномору~
0 h 100
~
if (%actor.vnum%>0) || !( %exist.mob(38408)% || %exist.mob(38419)% )
  halt
end
wait 1s
emote оторвался от дум и тяжело взглянул на вас..
wait 1s
say Не в лучшие времена ты пожаловал к нам, странник.
say Что привело тебя к нам, охраняющим покой славного острова Буян?
wait 1s
say Да, признаться, этот долг что-то становится нам в тягость...
вздохнуть
wait 1s
say Так вот, коли помочь ты желаешь, так и скажи, и нечего у меня время отнимать, и так дел невпроворот.
~
#38402
продолжение~
0 d 0
желаю~
wait 1s
улыб
say Похвально, что не перевелись еще смельчаки на Руси. Вот что
say происходит у нас нынче... Чья-то воля погнала на наш остров
say чудище морское, невиданное, да такое, что даже мы, витязи
say морские не в силах бываем совладать с ним. Каждый раз теряем
say мы все больше наших воинов доблестных, но удается лишь
say отогнать его на время. И самая главная беда в том...
руг
say ... что связаны мы клятвой никогда не покидать рубежей
say славного острова, а посему не можем мы настичь и покончить с
say чудищем в дальнем море.
wait 1s
say Коли есть в тебе воля помочь нам, и силы чтобы победить
say чудо-юдо морское, сделай доброе дело, и ты не пожалеешь.
wait 2s
say ...Да, вот еще что, коли не обманывает меня память,
say живет где-то неподалеку один маг...
say Правда, может быть он уже умер, ведь столько лет прошло с нашей
say последней встречи, да и врагов у него было немало...
say Ну дык дело не в этом... он мог бы помочь тебе справиться с 
say чудищем, коли не под силу оно тебе окажется.
say Разыщи его, если будет нужда, да скажи слова заветные
say "смерть владыке морскому".
wait 1s
эмоц перекрестил вас и безнадежно махнул рукой, погрузившись в раздумья
~
#38403
дали вещдок~
0 j 100
~
wait 1
if ( %object.vnum% != 38420 ) && ( %object.vnum% != 38421 )
  вопр
  give %object.name% .%actor.name%
  halt
end
wait 1
%purge% %object%
wait 1s
удив
wait 1s
say Вот так дела. Глазам своим не верю! Неужто и впрямь одолел
say ты врага нашего... Зря сомневался я в тебе. За спасение
say острова славного, Буяна награжу я тебя, как обещал, ничего
say не пожалею.
eval rnd 5+%actor.remort%*3
if ( %actor.class% == 1 ) && ( %random.100% <= %rnd% ) 
  if !%actor.spelltype(переход)%
    %spellturn% %actor.name% переход set
    halt
  end
end
%actor.gold(+20000)%
%send% %actor% Дядька Чернномор дал вам огромную кучу кун.
~
#38404
речь мага1~
0 c 100
говори~
wait 2s
say Ааа, один из тех, кого Черномор отправил на безнадежную борьбу
say с чудищем морским? Да, да, это в его духе.
wait 1s
хих
wait 1s
say Ну да ладно, не суть. И на чудо-рыбу управу найдем, было бы
say желание. Я пожалуй мог бы помочь тебе в этом.
say Но и ты, будь добр, сослужи мне службу...
wait 1s
say Есть у меня враг смертный, Владыка Морской. Славно бились мы
say с ним в свое время, да сам видишь, кто победил. Молод я был тогда
say и поклялся, что не уйду в мир теней, пока не отомщу, а сил на это
say таких давно уж как не осталось...
wait 1s
груст
wait 1s
say Убей Владыку и принеси мне его символ власти, тогда и поговорим...
wait 1s
эмоц отвернулся, давая понять, что разговор окончен
wait 4s
%echo% Через пару секунд призрак начал медленно исчезать...
if %exist.mob(38420)%
  calcuid mage 38420 mob
  %purge% %mage%
end
~
#38405
summon~
2 d 100
смерть владыке морскому~
if !%exist.mob(38420)%
  %echo% Призрак мага медленно появился из пустоты.
  %load% mob 38420
  wait 1s
  calcuid mage 38420 mob
  calcuid weapon 33300 obj
  if %weapon.room%!=33384
    %force% %mage% дум
    halt
  end
  exec 38404 %mage.id%
end
~
#38406
purge рыбы на репопе~
0 n 100
~
if %exist.mob(38419)%
  calcuid tmp 38419 mob
  %purge% %tmp%
end
~
#38407
transformation~
0 m 999999999
~
mtransform 38419
~
#38408
purge мага на репопе~
2 f 100
~
if %exist.mob(38420)%
  calcuid tmp 38420 mob
  %purge% %tmp%
end
~
#38409
дали~
0 j 100
~
if %object.vnum% != 33300
  спл
  halt
end
wait 1
%purge% морской.трезубец
wait 2s
say Наконец-то! Ты убил моего древнего врага, и я в долгу перед тобой...
wait 1s
say Ты шел убивать там рыбку какую-то?
wait 1s
эмоц презрительно усмехнулся
wait 1s
say Посмотрим, как она теперь драться будет...
wait 1s
эмоц воздел полупрозрачные руки и произнес заклинание, от силы которого содрогнулись своды пещеры
wait 2s
say Теперь ступай, наша сделка закончена. Да передай привет Черномору...
if !%exist.mob(38419)%
  if %exist.mob(38408)%
    calcuid lfish 38408 mob
    attach 38407 %lfish.id%
    exec 38407 %lfish.id%
    detach 38407 %lfish.id%
  end
end
~
#38410
отрезать вещдок~
2 c 0
отре отрез отреза отрезат отрезать отру отруб отруби отрубит отрубить выр вырв вырва вырват вырвать~
if !(%arg.contains(зуб)% || %arg.contains(плавник)%) 
  %send% %actor% Что вы точно собираетесь делать?
  halt
end
if %arg.contains(зуб)%
  %send% %actor% Вы отделили огромный осколок зуба от туши.
  %echoaround% %actor% %actor.name% отделил%actor.g% огромный осколок зуба от туши.
  calcuid body 38422 obj
  %purge% %body%
  wload obj 38420
else
  %send% %actor% Вы отделили огромный кусок плавника от туши.
  %echoaround% %actor% %actor.name% отделил%actor.g% огромный кусок плавника от туши.
  calcuid body 38422 obj
  %purge% %body%
  wload obj 38421
end
detach 38410 %self.id%
~
#38411
рыба сдохла~
0 f 100
~
%load% obj 38422
calcuid deathroom %self.realroom% room
attach 38410 %deathroom.id%
if ((%world.curobjs(3333)% < 1) && (%world.curobjs(3332)% < 1) && (%random.1000% < 90))
  mload obj 3332
end
~
#38412
death2~
1 c 100
deadbody~
%echo% test
~
#38413
помер Черномор - лоад сетшмотки~
0 f 100
~
if (%world.curobjs(3318)% < 1 && (%random.1000% <= 20))
  mload obj 3318
end
~
$~
